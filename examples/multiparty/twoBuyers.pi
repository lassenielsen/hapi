// twoBuyers protocol: Participants: 1 is Buyer1, 2 is Seller and 3 is Buyer2 // {{{
#define $twoBuyers \
  1->2: String; /* title */ \
  2->1: Int; /* quote */ \
  2->3: Int; /* quote */ \
  1->3: Int; /* contrib */ \
  3->2 {^ok: 3->2: String; /* address */ \
             2->3: String; /* date */ \
             $end; \
        ^quit: $end; \
       }
// }}}
global $twoBuyers twoBuyers(1,2,3);
b1 = new twoBuyers(1 of 3); // Buyer1 {{{
Int maxContrib=50;
b1[2]<<"BookTitle";
b1[2]>>quote;
if quote<=2*maxContrib
then b1[3]<<quote/2;
else b1[3]<<maxContrib; // }}}
|
s = new twoBuyers(2 of 3); // Seller {{{
s[1]>>title;
s[1]<<75; // Calc price from title
s[3]<<75;
s[3]>>
{^ok:
  s[3]>>dest;
  s[3]<<"Tomorrow";
 ^quit:
} // }}}
|
b2 = new twoBuyers(3 of 3); // Buyer2 {{{
Int maxContrib=90;
b2[2]>>quote;
b2[1]>>contrib;
if quote-contrib<=maxContrib
then b2[2]<<^ok<<"My address";
     b2[2]>>date;
else b2[2]<<^quit; // }}}
