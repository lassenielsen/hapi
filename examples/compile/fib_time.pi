#include <console.pi>
#include <sys/timer.pi>

#define $fib \
  2->1:Int; \
  1->2:Int; \
  $end;

#define $fibtime \
  2->1:Int; \
  1->2:Int; \
  1->2:Float; \
  $end;

global $fib fib(1 pure,2);
local pure Fib()
( global s = new fib(1 of 2);
  Fib(); |
  s[2] >> x;
  if x<=1
  then s[2] << 1;
  else s1 = new fib(2 of 2);
       s1[1] << x-1;
       s1[1] >> f1;
       s2=new fib(2 of 2);
       s2[1] << x-2;
       s2[1] >> f2;
       s[2] << f1+f2;
)
local pure StartFib(Int c)
( if c<=0
  then Fib();
  else ( Fib(); | StartFib(c-1); )
)
StartFib(SYSTEM&"tprocs"); |
global $fibtime timefib(1,2);
local TimeFib()
( global s = new timefib(1 of 2);
  TimeFib(); |
  s[2] >> n;
  t = new timer(2 of 2);
  f = new fib(2 of 2);
  f[1] << n;
  f[1] >> fn;
  s[2] << fn;
  t[1] << ^read;
  t[1] >> secs;
  t[1] << ^end;
  s[2] << secs;
)
TimeFib(); |
c = new console(2 of 2);
c[1] << ^str << "What is n? "
     << ^input << ^int;
c[1] >> v;
f = new timefib(2 of 2);
f[1] << v;
f[1] >> r;
f[1] >> secs;
c[1] << ^str << "Fib(n) is "
     << ^int << r << ^nl
     << ^str << "Used time "
     << ^flt << secs
     << ^str << " seconds"
     << ^nl << ^end;
