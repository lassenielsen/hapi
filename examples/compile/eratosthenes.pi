#include<console.pi>
#include<struct/list.pi>
#include<callable.pi>
#include<succ.pi>
//#include<debug/procmon.pi>

#define $filtermultipla \
  2->1:Int; \
  $callable(Int,Bool)
global $filtermultipla filtermultipla(1 pure,2);
local pure service FilterMultipla(filtermultipla (1 of 2) s)
( s[2]>>d;
  local pure Filter($callable(Int,Bool)(1 of 1 pure,2) this)
  ( this[2]>>
    {^call:
      this[2]>>n;
      this[2]<<not ((n/d)*d=n);
      Filter(this);
     ^end:
    }
  )
  Filter(s);
)
|
global Eratosthenes($list(Int)(2 of 1 pure,2) list)
( list[1]<<^pop;
  list[1]>>
  {^nil:
   ^cons:
    list[1]>>p>>tail;
    c=new console(2 of 2);
    c[1]<<^int<<p<<^nl<<^end;
    filter=new filtermultipla(2 of 2);
    filter[1]<<p;
    tail[1]<<^filter<<filter;
    Eratosthenes(tail);
  }
)
ilist=new range(2 of 2);
ilist<<Int;
isucc=new stdsucc_Int(2 of 2);
iorder=new stdorder_Int(2 of 2);
ilist[1]<<isucc<<iorder<<2<<20000;
Eratosthenes(ilist);
