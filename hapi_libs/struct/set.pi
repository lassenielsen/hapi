#include<order.pi>
#define $set(_t) \
  rec $set; \
  2->1 \
  {^end: $end; \
   ^assign: 2->1: _t; 2->1: $set;(2 of 1 pure,2); 2->1: $set;(2 of 1 pure,2); $set; \
   ^insert: 2->1: _t; $set; \
   ^member: 2->1: _t; 1->2: Bool; $set; \
   ^size: 1->2: Int; $set; \
   ^height: 1->2: Int; $set; \
   ^pop: \
    1->2 \
    {^nil: $end; \
     ^tree: \
      1->2: _t; \
      1->2: $set;(2 of 1 pure, 2); \
      1->2: $set;(2 of 1 pure, 2); \
      $end; \
    } \
   ^copy: \
    1->2: $set;(2 of 1 pure,2); \
    $set; \
  }
#define $absset \
  2: #elt; \
  2->1: $order(#elt)(1 pure, 2); \
  $set(#elt)

global $absset set(1 pure, 2);
local pure service SetService(set (1 of 2) s) // {{{
( s>>#t;
  s[2]>>chcmp;
  local pure NilSet($set(#t)(1 of 1 pure,2) this) // {{{
  ( local pure Set($set(#t)(1 of 1 pure, 2) this, #t member, $set(#t)(2 of 1 pure, 2) left, $set(#t)(2 of 1 pure, 2) right, Int height, $order(#t)(2 of 1 pure,2) cmp) // {{{
    ( local pure RotateLeft($set(#t)(1 of 1 pure, 2) this, #t member, $set(#t)(2 of 1 pure, 2) left, $set(#t)(2 of 1 pure, 2) right, $order(#t)(2 of 1 pure,2) cmp) // {{{
      ( right[1]<<^pop;
        right[1]>>
        {^nil:
          // Cannot rotete
          right=new set(2 of 2);
          right<<#t;
          right[1]<<chcmp;
          left[1]<<^height;
          left[1]>>lh;
          Set(this,member,left,right,1+lh,cmp);
         ^tree:
          right[1]>>rmember;
          right[1]>>rLeft;
          right[1]>>rRight;
          s=new set(2 of 2);
          s<<#t;
          s[1]<<chcmp;
          s[1]<<^assign<<member<<left<<rLeft;
          s[1]<<^height;
          s[1]>>lh;
          rRight[1]<<^height;
          rRight[1]>>rh;
          Set(this,rmember,s,rRight,if lh<=rh then 1+rh else 1+lh,cmp);
        }
      ) // }}}
      local pure RotateRight($set(#t)(1 of 1 pure, 2) this, #t member, $set(#t)(2 of 1 pure, 2) left, $set(#t)(2 of 1 pure, 2) right, $order(#t)(2 of 1 pure,2) cmp) // {{{
      ( left[1]<<^pop;
        left[1]>>
        {^nil:
          // Cannot rotate
          left=new set(2 of 2);
          left<<#t;
          left[1]<<chcmp;
          right[1]<<^height;
          right[1]>>rh;
          Set(this,member,left,right,1+rh,cmp);
         ^tree:
          left[1]>>lmember;
          left[1]>>lLeft;
          left[1]>>lRight;
          s=new set(2 of 2);
          s<<#t;
          s[1]<<chcmp;
          s[1]<<^assign<<member<<lRight<<right;
          lLeft[1]<<^height;
          lLeft[1]>>lh;
          s[1]<<^height;
          s[1]>>rh;
          Set(this,lmember,lLeft,s,if lh<=rh then rh+1 else lh+1,cmp);
        }
      ) // }}}
      local pure Balance($set(#t)(1 of 1 pure, 2) this, #t member, $set(#t)(2 of 1 pure, 2) left, $set(#t)(2 of 1 pure, 2) right, $order(#t)(2 of 1 pure,2) cmp) // {{{
      ( left[1]<<^pop;
        left[1]>>
        {^nil:
          left=new set(2 of 2);
          left<<#t;
          left[1]<<chcmp;
          right[1]<<^pop;
          right[1]>>
          {^nil:
            right=new set(2 of 2);
            right<<#t;
            right[1]<<chcmp;
            Set(this,member,left,right,1,cmp);
           ^tree:
            right[1]>>rmember;
            right[1]>>rLeft;
            right[1]>>rRight;
            s=new set(2 of 2);
            s<<#t;
            s[1]<<chcmp;
            s[1]<<^assign<<member<<left<<rLeft;
            s[1]<<^height;
            s[1]>>lh;
            rRight[1]<<^height;
            rRight[1]>>rh;
            Set(this,rmember,s,rRight,if lh<=rh then rh+1 else lh+1,cmp);
          }
         ^tree:
          left[1]>>lmember;
          left[1]>>lLeft;
          left[1]>>lRight;
          right[1]<<^pop;
          right[1]>>
          {^nil:
            right=new set(2 of 2);
            right<<#t;
            right[1]<<chcmp;
            s=new set(2 of 2);
            s<<#t;
            s[1]<<chcmp;
            s[1]<<^assign<<member<<lRight<<right;
            lLeft[1]<<^height;
            lLeft[1]>>lh;
            s[1]<<^height;
            s[1]>>rh;
            Set(this,lmember,lLeft,s,if lh<=rh then rh+1 else lh+1,cmp);
           ^tree:
            right[1]>>rmember;
            right[1]>>rLeft;
            right[1]>>rRight;
            lLeft[1]<<^height;
            lLeft[1]>>hll;
            lRight[1]<<^height;
            lRight[1]>>hlr;
            rLeft[1]<<^height;
            rLeft[1]>>hrl;
            rRight[1]<<^height;
            rRight[1]>>hrr;
            hl=if hll<=hlr then hlr else hll;
            hr=if hrl<=hrr then hrr else hrl;
            s1=new set(2 of 2);
            s1<<#t;
            s1[1]<<chcmp;
            s2=new set(2 of 2);
            s2<<#t;
            s2[1]<<chcmp;
            if (hll+1<=hlr) and (2+hr<=hl)
            then s1[1]<<^assign<<lmember<<lLeft<<lRight;
                 s2[1]<<^assign<<rmember<<rLeft<<rRight;
                 RotateRight(this,member,s1,s2,cmp);
            else if 2+hr<=hl
            then s1[1]<<^assign<<lmember<<lLeft<<lRight;
                 s2[1]<<^assign<<rmember<<rLeft<<rRight;
                 RotateRight(this,member,s1,s2,cmp);
            else if (hrr+1<=hrl) and (hl+2<=hr)
            then s1[1]<<^assign<<lmember<<lLeft<<lRight;
                 s2[1]<<^assign<<rmember<<rLeft<<rRight;
                 RotateLeft(this,member,s1,s2,cmp);
            else if hl+2<=hr
            then s1[1]<<^assign<<lmember<<lLeft<<lRight;
                 s2[1]<<^assign<<rmember<<rLeft<<rRight;
                 RotateLeft(this,member,s1,s2,cmp);
            else s1[1]<<^assign<<lmember<<lLeft<<lRight;
                 s2[1]<<^assign<<rmember<<rLeft<<rRight;
                 Set(this,member,s1,s2,if hl<=hr then hr+1 else hl+1,cmp);
          }
        }
      ) // }}}
      this[2]>>
      { ^end:
          left[1]<<^end;
          right[1]<<^end;
          cmp[1]<<^end;
        ^assign:
          this[2]>>m;
          this[2]>>l;
          this[2]>>r;
          left[1]<<^end;
          right[1]<<^end;
          l[1]<<^height;
          l[1]>>hl;
          r[1]<<^height;
          r[1]>>hr;
          Set(this,m,l,r,if hr<=hl then 1+hl else 1+hr,cmp);
        ^insert:
          this[2]>>v;
          cmp[1]<<^eq<<v<<member;
          cmp[1]>>eq;
          if eq
          then Set(this,member,left,right,height,cmp);
          else cmp[1]<<^leq<<v<<member;
               cmp[1]>>leq;
               if leq
               then left[1]<<^insert<<v; Balance(this,member,left,right,cmp);
               else right[1]<<^insert<<v; Balance(this,member,left,right,cmp);
        ^member:
          this[2]>>v;
          cmp[1]<<^eq<<v<<member;
          cmp[1]>>eq;
          if eq
          then this[2]<<true;
               Set(this,member,left,right,height,cmp);
          else cmp[1]<<^leq<<v<<member;
               cmp[1]>>leq;
               if leq
               then left[1]<<^member<<v;
                    left[1]>>r;
                    this[2]<<r;
                    Set(this,member,left,right,height,cmp);
               else right[1]<<^member<<v;
                    right[1]>>r;
                    this[2]<<r;
                    Set(this,member,left,right,height,cmp);
        ^size:
          left[1]<<^size;
          left[1]>>ls;
          right[1]<<^size;
          right[1]>>rs;
          this[2]<<1+ls+rs;
          Set(this,member,left,right,height,cmp);
        ^height:
          this[2]<<height;
          Set(this,member,left,right,height,cmp);
        ^pop:
          this[2]<<^tree;
          this[2]<<member;
          this[2]<<left;
          this[2]<<right;
          cmp[1]<<^end;
         ^copy:
          $set(#t) s1,s2(1 pure,2);
          left[1]<<^copy;
          left[1]>>leftcpy;
          right[1]<<^copy;
          right[1]>>rightcpy;
          cmp2=new chcmp(2 of 2);
          this[2]<<s2;
          ( Set(s1,member,leftcpy,rightcpy,height,cmp2);
          | Set(this,member,left,right,height,cmp);
          )
      }
    ) // }}}
    this[2]>>
    {^end:
     ^assign:
      this[2]>>m;
      this[2]>>l;
      this[2]>>r;
      l[1]<<^height;
      l[1]>>lh;
      r[1]<<^height;
      r[1]>>rh;
      cmp=new chcmp(2 of 2);
      Set(this,m,l,r,if lh<=rh then 1+rh else 1+lh,cmp);
     ^insert:
      lft=new set(2 of 2);
      lft<<#t;
      lft[1]<<chcmp;
      rgt=new set(2 of 2);
      rgt<<#t;
      rgt[1]<<chcmp;
      cmp=new chcmp(2 of 2);
      this[2]>>v;
      Set(this,v,lft,rgt,1,cmp);
     ^member:
      this[2]>>q;
      this[2]<<false;
      NilSet(this);
     ^size:
      this[2]<<0;
      NilSet(this);
     ^height:
      this[2]<<0;
      NilSet(this);
     ^pop:
      this[2]<<^nil;
     ^copy:
      $set(#t) s1,s2(1 pure,2);
      this[2]<<s2;
      ( NilSet(s1);
      | NilSet(this);
      )
    }
  ) // }}}
  NilSet(s);
) | // }}}
